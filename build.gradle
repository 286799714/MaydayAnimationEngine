plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.maydaymemory.mae'
version = '1.0.3'

compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation('org.joml:joml:1.10.5')
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation("it.unimi.dsi:fastutil:8.5.15")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

java{
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.maydaymemory'
            artifactId = "mae"
            version = project.version

            pom {
                name = 'Mayday Animation Engine'
                description = 'A java runtime animation library'
                url = 'https://github.com/286799714/MaydayAnimationEngine'

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'maydaymemory'
                        name = 'MaydayMemory'
                        email = 'maydaymemory123@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/286799714/MaydayAnimationEngine.git'
                    developerConnection = 'scm:git:ssh://github.com/286799714/MaydayAnimationEngine.git'
                    url = 'https://github.com/286799714/MaydayAnimationEngine'
                }
            }
        }
    }

    repositories {
        maven {
            name = "localBuildRepo"
            url = uri("$buildDir/maven-repo")
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

// Remove publishToMavenLocal's dependency on signing task
plugins.withId('maven-publish') {
    afterEvaluate {
        tasks.named('publishToMavenLocal') {
            dependsOn.removeAll { dep ->
                // Remove all signature related dependencies
                def task = dep instanceof Task ? dep : findProperty('dep')?.get() as Task
                task?.name?.startsWith('sign') ?: false
            }
        }

        // Ensure signing task is not executed when publishingToMavenLocal
        gradle.taskGraph.whenReady { graph ->
            if (graph.hasTask(':publishToMavenLocal')) {
                tasks.withType(Sign).configureEach { signTask ->
                    signTask.enabled = false
                }
            }
        }
    }
}